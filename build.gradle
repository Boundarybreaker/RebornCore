buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7


repositories {
    maven {
        name = "Modmuss50"
        url = "http://maven.modmuss50.me/"
    }
}

configurations {
    shade
    compile.extendsFrom shade
}

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = "1.2.1." + "${System.getenv().BUILD_NUMBER}"
} else {
    version = "1.2.1.BUILD"
}

minecraft {
    version = "1.8.8-11.15.0.1625-1.8.8"
    mappings = 'snapshot_20151129'
    replace "@MODVERSION@", project.version
	runDir = "run"
}

group = 'RebornCore'

processResources
        {
            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'
                expand 'version':project.version, 'mcversion':project.minecraft.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }


task deobfJar(type: Jar) {
    from sourceSets.main.output
    exclude "**/*.psd"
    classifier = 'dev'
}

jar {
    exclude "**/*.psd"
    classifier = 'universal'
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.main.output
        include 'techreborn/api/**/*'

    classifier = "api"
}

apiJar.mustRunAfter deobfJar

artifacts {
    archives deobfJar
    archives apiJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:///var/www/maven/")
            pom {
                groupId = "RebornCore"
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'RebornCore'
                    url 'https://github.com/TechReborn/RebornCore'
                    scm {
                        url 'https://github.com/TechReborn/RebornCore'
                        connection 'scm:git:git@github.com:TechReborn/RebornCore.git'
                        developerConnection 'scm:git:git@github.com:TechReborn/RebornCore.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/TechReborn/RebornCore/issues'
                    }
                    licenses {
                        license {
                            name 'License'
                            url 'https://raw.githubusercontent.com/TechReborn/RebornCore/master/LICENSE.md'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'modmuss50'
                            name 'modmuss50'
                            roles { role 'developer' }
                        }
                        developer {
                            id 'Gigabit101'
                            name 'Gigabit101'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    //To update the wrapper change the version bellow then run: gradle wrapper
    gradleVersion = '2.8'
}
